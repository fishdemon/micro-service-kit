plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

// set dependencies of all projects
allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    group 'com.fishdemon.msk'
    version '1.0'

    sourceCompatibility = 1.8

    repositories {
        maven{ url 'https://maven.aliyun.com/repository/jcenter'}
        maven{ url 'https://maven.aliyun.com/repository/gradle-plugin'}
        maven{ url 'https://maven.aliyun.com/repository/public'}
        mavenCentral()
    }
}

// set dependencies of sub projects
subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    ext {
        set('springBootAdminVersion', "2.2.1")
        set('springCloudVersion', "Hoxton.SR3")
    }

    dependencies {
//        implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        runtimeOnly 'org.springframework.boot:spring-boot-devtools'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    dependencyManagement {
        imports {
            mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }
}

project(':msk-eureka') {
    dependencies {
	    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
    }
}

project(':msk-boot-admin') {
    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        implementation 'de.codecentric:spring-boot-admin-starter-server'
        implementation 'de.codecentric:spring-boot-admin-server-ui'
    }
}

project(':msk-config') {
    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.cloud:spring-cloud-config-server'
    }
}

project(':msk-gateway') {
    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-zuul'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    }
}

project(':msk-auth') {
    dependencies {
        // cloud
//        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
//        implementation 'org.springframework.boot:spring-boot-starter-actuator'
//        implementation 'org.springframework.cloud:spring-cloud-starter-config'
//        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        // mvc
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        implementation 'org.springframework.security:spring-security-jwt:1.0.10.RELEASE'
        // swagger
        implementation 'io.springfox:springfox-swagger2:2.4.0'
        implementation 'io.springfox:springfox-swagger-ui:2.4.0'
        implementation 'com.github.xiaoymin:swagger-bootstrap-ui:1.9.6'
        // hutool
        implementation 'cn.hutool:hutool-all:5.2.5'
        // mybatis plus
        implementation 'com.baomidou:mybatis-plus-boot-starter:3.2.0'
        implementation 'org.freemarker:freemarker:2.3.29'
        implementation 'com.baomidou:mybatis-plus-generator:3.2.0'
        // mysql
        runtimeOnly 'mysql:mysql-connector-java'
        // ip
        implementation 'org.lionsoul:ip2region:1.7.2'
        implementation 'commons-io:commons-io:2.7'
    }
}

project(':msk-provider') {
    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
}

project(':msk-consumer') {
    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-hystrix-dashboard'
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
}

project(':msk-zipkin') {
    ext {
        set('zipkinVersion', "2.10.1")
    }
    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        implementation "io.zipkin.java:zipkin:${zipkinVersion}"
        implementation ("io.zipkin.java:zipkin-server:${zipkinVersion}") {
            exclude module: 'log4j-slf4j-impl'
        }
        implementation "io.zipkin.java:zipkin-autoconfigure-ui:${zipkinVersion}"
    }
}

